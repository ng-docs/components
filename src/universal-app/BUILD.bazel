load("@build_bazel_rules_nodejs//:index.bzl", "nodejs_test")
load("//src/cdk:config.bzl", "CDK_TARGETS")
load("//src/cdk-experimental:config.bzl", "CDK_EXPERIMENTAL_TARGETS")
load("//src/material:config.bzl", "MATERIAL_TARGETS")
load("//src/material-experimental:config.bzl", "MATERIAL_EXPERIMENTAL_TARGETS")
load("//tools:defaults.bzl", "devmode_esbuild", "ng_module", "sass_binary", "ts_library")
load("//tools/angular:index.bzl", "LINKER_PROCESSED_FW_PACKAGES")

package(default_visibility = ["//visibility:public"])

ng_module(
    name = "kitchen-sink",
    srcs = [
        "kitchen-sink-mdc/kitchen-sink-mdc.ts",
        "kitchen-sink-root.ts",
        "kitchen-sink/kitchen-sink.ts",
    ],
    assets = [
        "kitchen-sink/kitchen-sink.html",
        "kitchen-sink-mdc/kitchen-sink-mdc.html",
    ],
    deps = [
        "@npm//@angular/platform-server",
        "//src/google-maps",
        "//src/youtube-player",
    ] + CDK_TARGETS + CDK_EXPERIMENTAL_TARGETS + MATERIAL_TARGETS + MATERIAL_EXPERIMENTAL_TARGETS,
)

ts_library(
    name = "server",
    srcs = [
        "prerender.ts",
    ],
    deps = [
        ":kitchen-sink",
        "@npm//@angular/platform-server",
        "@npm//@bazel/runfiles",
        "@npm//@types/node",
        "@npm//reflect-metadata",
        "@npm//zone.js",
    ],
)

sass_binary(
    name = "theme_scss",
    src = "theme.scss",
    deps = [
        "//src/material:sass_lib",
        "//src/material-experimental:sass_lib",
    ],
)

devmode_esbuild(
    name = "server_bundle",
    entry_point = ":prerender.ts",
    platform = "node",
    # We cannot use `ES2017` or higher as that would result in `async/await` not being downleveled.
    # ZoneJS needs to be able to intercept these as otherwise change detection would not work properly.
    target = "es2016",
    # Note: We add all linker-processed FW packages as dependencies here so that ESBuild will
    # map all framework packages to their linker-processed bundles from `tools/angular`.
    deps = LINKER_PROCESSED_FW_PACKAGES + [
        ":server",
    ],
)

nodejs_test(
    name = "server_test",
    data = [
        "index.html",
        ":server_bundle",
        ":theme_scss",
    ],
    entry_point = ":server_bundle.js",
    templated_args = [
        # TODO(josephperrott): update dependency usages to no longer need bazel patch module resolver
        # See: https://github.com/bazelbuild/rules_nodejs/wiki#--bazel_patch_module_resolver-now-defaults-to-false-2324
        "--bazel_patch_module_resolver",
    ],
)
